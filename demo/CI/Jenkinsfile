@Library("mylib@main") _
import org.devops.*

// ========== 工具类初始化 ==========
def checkout = new Checkout()
def buildTool = new Build()
def unittest = new UnitTest()
def notified = new Notified()
def artifacts = new Artifacts()
def sonar = new Sonar()


// ========== 配置变量 ==========
def DEFAULT_SRC_URL = 'http://192.168.5.85:8802/ops/devops03-maven-service.git'
def DEFAULT_SRC_BRANCH = 'main'
def DEFAULT_CONFIG_URL = 'http://192.168.5.85:8802/ops/ops-jenkinslib.git'
def DEFAULT_CONFIG_BRANCH = 'main'
def DEFAULT_USER_EMAIL = ''

// ========== 应用变量 ==========
def app = ['project':'devops', 'appName':'devops03-maven-service', 'appType': 'maven', 'appModule': '', 'mavenDeploy': true, 'nexusPush': true, 'dockerBuild': true, 'sonarProject': 'devops03-maven-service']

// ========== 下游任务 ==========
def jobs = ['项目01/TST/devops03-maven-service']

try {
    //gitlab传递的数据
    println("${WebhookData}")

    //数据格式化
    webHookData = readJSON text: "${WebhookData}"

    //提取仓库信息
    env.webhook_srcUrl = webHookData["project"]["git_http_url"]     //项目地址
    env.webhook_branchName = webHookData["ref"] - "refs/heads/"    //分支
    env.webhook_commitId = webHookData["checkout_sha"]             //提交id
    env.webhook_commitTitle = webHookData["title"]             //提交描述
    env.webhook_commitUser = webHookData["user_username"]           //提交人
    env.webhook_userEmail = webHookData["user_email"]               //邮箱

 } catch(e){
    print(e)
 }


pipeline {
    agent { label "build" }

    triggers {
        GenericTrigger(
            causeString: 'Generic Cause',
            genericVariables: [
                [defaultValue: '',key: 'WebhookData',regexpFilter: '',value: '$'],
                [defaultValue: '', key: 'after', regexpFilter: '', value: '$.after']
            ],
            regexpFilterExpression: '^(?!0{40}).{40}$',
            regexpFilterText: '$after',
            token: 'devops03-maven-service',
            tokenCredentialId: ''
        )
    }
    options {
        skipDefaultCheckout true
        timestamps()
    }

    parameters {
        // string(name: 'PARAMS_SRC_URL', defaultValue: DEFAULT_SRC_URL, description: '源代码仓库URL')
        choice(
        name: 'PARAMS_SRC_URL',
        choices: [
            DEFAULT_SRC_URL
        ],
        description: '源代码仓库URL'
    )
        string(name: 'PARAMS_SRC_BRANCH', defaultValue: DEFAULT_SRC_BRANCH, description: '代码分支')
    }

    environment {
        // 将参数转为环境变量 并固定无法更改
        SRC_URL = "${env.webhook_srcUrl ?: params.PARAMS_SRC_URL}"
        SRC_BRANCH = "${env.webhook_branchName ?: params.PARAMS_SRC_BRANCH}"
        CONF_URL = "${DEFAULT_CONFIG_URL}"
        CONF_BRANCH = "${DEFAULT_CONFIG_BRANCH}"
    }

    stages {
        stage("Checkout"){
            steps {
                cleanWs()
                dir('config'){
                    script {
                        checkout.GetCode("${env.CONF_URL}", "${env.CONF_BRANCH}")
                    }
                }
                dir('code'){
                    script {
                        def checkoutResult = checkout.GetCode("${env.SRC_URL}", "${env.SRC_BRANCH}")
                        env.SRC_COMMIT_ID = checkoutResult.shortCommitId
                        env.SRC_COMMIT_TITLE = checkoutResult.title
                        env.SRC_COMMITTER = checkoutResult.committer
                        env.ARTIFACT_VERSION = checkoutResult.version
                    }
                }

            }
        }

        stage("PrepareConfig"){
            steps {
                script {
                    echo "${env.WORKSPACE}"
                    sh "cp -r ${env.WORKSPACE}/config/${env.JOB_NAME}/* ${env.WORKSPACE}/code/"
                }
            }
        }

        stage("CodeBuild"){
            steps {
                dir('code'){
                    script {
                        buildTool.CodeBuild("${app.appType}","${app.appModule}")
                    }
                }
            }
        }

        stage("UnitTest"){
            steps {
                dir('code'){
                    script {
                        unittest.CodeTest("${app.appModule}")
                    }
                }
            }
        }

        stage('PushArtifacts'){
            steps {
                dir('code') {
                    script {                                       
                        // 上传到 Maven 仓库
                        if(app.mavenDeploy){
                            artifacts.DeployMavenArtifact(app.appModule)
                        }
                        // 上传到 Nexus raw 仓库
                        // 更完整的语义化版本验证 (SemVer)
                        // def isVersionValid = env.ARTIFACT_VERSION ==~ /^v?(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-([0-9A-Za-z-]+(?:\.[0-9A-Za-z-]+)*))?(?:\+([0-9A-Za-z-]+(?:\.[0-9A-Za-z-]+)*))?$/
                        // 或者自定义的简单版本号验证
                        // def isVersionValid = env.ARTIFACT_VERSION ==~ /^v?\d+\.\d+\.\d+(-\w+)?$/
                        if(app.nexusPush){
                            artifacts.PushRawArtifacts(app.project, app.appName, app.appType, app.appModule)
                        }
                        // 上传到 Harbor 镜像仓库
                        if(app.dockerBuild){
                            artifacts.PushDockerArtifacts(app.project, app.appName, env.ARTIFACT_VERSION)
                        }     
                    }

                }
            }
        }

        stage('QA'){
            when {
                allOf {
                    expression {
                        app.sonarProject
                    }
                }
            }
            
            steps{
                dir('code') {
                    script{
                        sonar.SonarScan(app.appType, app.sonarProject, app.sonarProject, env.ARTIFACT_VERSION, DEFAULT_SRC_URL)
                    }
                }
            }
        }

        stage('Trigger Downstream') {
            steps {
                script {
                    jobs.each { job -> 
                        build(
                            job: job,
                            parameters: [
                                string(name: 'releaseVersion', value: env.ARTIFACT_VERSION)
                            ],
                            wait: false
                        )
                    }
                }
            }
        }
    }

    post {
        always{
            wrap([$class: 'BuildUser']) {
                script {
                    def causes = currentBuild.getBuildCauses()
                    env.triggerDescription = causes ? causes[0].shortDescription : "未知触发"

                    // 设置构建描述
                    currentBuild.description = """                           
                        Title: ${env.SRC_COMMIT_TITLE}
                        Branch: ${env.SRC_BRANCH}
                        CommitID: ${env.SRC_COMMIT_ID}
                        Committer: ${env.webhook_commitUser ?: env.SRC_COMMITTER }
                        Version: ${env.ARTIFACT_VERSION}
                    """.stripIndent().trim()
                    currentBuild.displayName = "#${env.BUILD_NUMBER} - ${env.triggerDescription}"
                    
                    // 发送构建通知
                    env.USER_EMAIL = "${env.webhook_userEmail ?: env.BUILD_USER_EMAIL ?: DEFAULT_USER_EMAIL}"
                    notified.SendEmail("${env.USER_EMAIL}")
                }
            }
        }        
    }
}